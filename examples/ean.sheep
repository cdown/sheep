# ean.sheep
#
# Copyright (c) 2009 Johannes Weiner

(function sum (seq)
  (reduce + seq))

(function diff-multiple (n mult)
  "Difference between N and the next multiple of MULT."
  (with (rest (% n mult))
    (if rest
      (- mult rest)
      rest)))

(function digits (n)
  "Make a list of digits in the number N."
  (map number (split "" (string n))))

(function ring-generator (items)
  "Create a ring generator that returns the items in
ITEMS sequentially in subsequent calls, restarting from
the beginning when reaching the end."
  (with (pos items)
    (function ()
      (if (not pos)
        (set pos items))
      (with (item (head pos))
        (set pos (tail pos))
        item))))

(function ring-multiplier (items)
  "Create a ring-multiplier, which is a function that
multiplies its sole argument with an item from ITEMS,
starting from the first and advancing the ring with
each subsequent invocation."
  (with (ring (ring-generator items))
    (function (n)
      (* n (ring)))))

(function ean-checksum (ean)
  "Checksum EAN: multiply each digit in the number
with 3 and 1 alternatingly, sum up the results and
take the difference from the next multiple of ten."
  (diff-multiple
    (sum (map (ring-multiplier (list 3 1))
              (reverse (digits ean))))
    10))

(function ean-checksum-range (base count)
  (if count
    (block
      (print base (ean-checksum base))
      (ean-checksum-range (+ base 1) (- count 1)))))

# xxx: should be able to check for being loaded or executed?  or just
# have the library in libean.sheep and the executable in ean.sheep?
(if (not (= (length argv) 3))
  (print "usage: " (nth 0 argv) " base count")
  (ean-checksum-range (number (nth 1 argv))
                      (number (nth 2 argv))))
